buildscript {
    apply from: 'https://moddingx.github.io/ModUtils/v4/buildscript.gradle', to: buildscript
}

apply from: 'https://moddingx.github.io/ModUtils/v4/mod.gradle'

apply plugin: 'org.moddingx.modgradle.coremods'

configurations {
    extra
    extraClasspath
    extraClasspath.extendsFrom extra
    extraClasspath.extendsFrom compileClasspath
    runtimeOnly.extendsFrom extra
}

dependencies {
    extraClasspath sourceSets.main.output
}

sourceSets {
    extra {
        compileClasspath = configurations.extraClasspath
    }
}

repositories {
    maven { url = 'https://maven.moddingx.org/' }
    maven { url = 'https://maven.melanx.de/' }
    maven { url = 'https://maven.blamejared.com/' }
    maven { url = 'https://maven.theillusivec4.top/' }
    maven { url = 'https://www.cursemaven.com' }
}

dependencies {
    annotationProcessor fg.deobf('org.moddingx:LibX:1.19.2-4.2.8')

    coremods 'org.moddingx:CoreModTypes:5.0.2-2'
    
    implementation fg.deobf('org.moddingx:LibX:1.19.2-4.2.8')
    implementation fg.deobf('io.github.noeppi_noeppi.mods:SandBox:1.19.2-1.0.1')
    implementation fg.deobf('vazkii.botania:Botania:1.19.2-437-FORGE')
    implementation fg.deobf('vazkii.patchouli:Patchouli:1.19.2-77')
    implementation fg.deobf('top.theillusivec4.curios:curios-forge:1.19.2-5.1.4.0')
    
    implementation fg.deobf("mezz.jei:jei-1.19.2-common-api:11.6.0.1015")
    implementation fg.deobf("mezz.jei:jei-1.19.2-forge-api:11.6.0.1015")
    runtimeOnly fg.deobf("mezz.jei:jei-1.19.2-forge:11.6.0.1015")

    extra fg.deobf("curse.maven:botanic-additions-310637:4508380") // BotanicAdditions-1.19.2-19.3.3.jar
    extra fg.deobf("curse.maven:hammer-lib-247401:4365804") // HammerLib-1.19.2-19.3.56.jar
}

compileJava.options.compilerArgs += [
        '-Amod.properties.strict_onlyin=true',
        '-Amod.properties.strict_super=true',
        '-Amod.properties.force_findbugs=true'
]

minecraft {
    runs {
        data {
            args '--existing-mod', 'botania'
            mods {
                "${project.modid}" {
                    source sourceSets.extra
                }
            }
        }
    }
}
